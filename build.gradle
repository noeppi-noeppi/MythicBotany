import org.apache.maven.artifact.versioning.ComparableVersion

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://dist.creeper.host/Sponge/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
//apply plugin: 'org.spongepowered.mixin'


group = 'io.github.noeppi_noeppi.mods'
archivesBaseName = 'MythicBotany'
version = getVersion(group, archivesBaseName, "1.16.3-1.0")

println version

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200723-1.16.1'/*'20200514-1.16'*/
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('runClient')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            jvmArg "-Dmixin.env.disableRefMap=true"

            mods {
                mythicbotany {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('runServer')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            jvmArg "-Dmixin.env.disableRefMap=true"

            mods {
                mythicbotany {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('runData')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            jvmArg "-Dmixin.env.disableRefMap=true"

            args '--mod', 'mythicbotany', '--all', '--output', file('src/generated/resources/'), "--existing", file("src/main/resources")

            mods {
                mythicbotany {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    maven {
        name = 'Forge'
        url = 'https://files.minecraftforge.net/maven/'
    }
    maven {
        // location of the maven that hosts JEI files
        name = 'Progwml6 maven'
        url = 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = 'ModMaven'
        url = 'https://modmaven.k-4u.nl'
    }
    maven {
        name = 'botania'
        url = 'https://maven.blamejared.com/'
    }
    maven {
        name = 'curios'
        url = 'https://maven.theillusivec4.top/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.3-34.1.0'

    compileOnly fg.deobf('mezz.jei:jei-1.16.2:7.3.2.27:api')
    compile fg.deobf('vazkii.botania:Botania:1.16.3-407-SNAPSHOT')
    compile fg.deobf('vazkii.patchouli:Patchouli:1.16-42')
    compile fg.deobf('top.theillusivec4.curios:curios-forge:1.16.3-4.0.2.0')

    runtimeOnly fg.deobf('mezz.jei:jei-1.16.2:7.3.2.27')
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                'Specification-Title'     : 'mythicbotany',
                'Specification-Vendor'    : 'noeppi_noeppi',
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : 'noeppi_noeppi',
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            artifact jar

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "/var/www/maven"
        }
    }
}

private static String getVersion(String group, String artifact, String baseVersion) {
    java.nio.file.Path mavenPath = Paths.get('/var/www/maven').resolve(group.replace('.', '/')).resolve(artifact)
    if (!Files.isDirectory(mavenPath)) {
        return "${baseVersion}.0"
    }
    return "${baseVersion}." + Files.walk(mavenPath)
            .filter{path -> Files.isRegularFile(path) && path.getFileName().toString().endsWith('.pom')}
            .map{path ->
                String fileName = path.getFileName().toString()
                fileName.substring(fileName.indexOf('-', artifact.size()) + 1, fileName.length() - 4)
            }.filter{version -> version.startsWith(baseVersion)}
            .max{s1, s2 -> new ComparableVersion(s1).compareTo(new ComparableVersion(s2))}
            .map{ver -> ver.substring(ver.findLastIndexOf{ str -> !"0123456789".contains(str) } + 1)}
            .map{ver -> ver.isEmpty() ? "-1" : ver }
            .map{ver -> (ver.toInteger() + 1).toString()}
            .orElse("0")
}