buildscript {
    apply from: 'https://moddingx.github.io/ModUtils/v5/buildscript.gradle', to: buildscript
}

apply from: 'https://moddingx.github.io/ModUtils/v5/mod.gradle'

apply plugin: 'org.moddingx.modgradle.coremods'

configurations {
    extra
    extraClasspath
    extraClasspath.extendsFrom extra
    extraClasspath.extendsFrom compileClasspath
    runtimeOnly.extendsFrom extra
}

dependencies {
    extraClasspath sourceSets.main.output
}

sourceSets {
    extra {
        compileClasspath = configurations.extraClasspath
    }
}

repositories {
    maven { url = 'https://maven.moddingx.org/' }
    maven { url = 'https://maven.melanx.de/' }
    maven { url = 'https://maven.blamejared.com/' }
    maven { url = 'https://maven.theillusivec4.top/' }
    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    annotationProcessor fg.deobf('org.moddingx:LibX:1.20.1-5.0.12')

    coremods 'org.moddingx:CoreModTypes:5.0.2-2'
    
    implementation fg.deobf('org.moddingx:LibX:1.20.1-5.0.12')
    implementation fg.deobf('vazkii.botania:Botania:1.20.1-443-FORGE')
    implementation fg.deobf('vazkii.patchouli:Patchouli:1.20.1-84-FORGE')
    implementation fg.deobf('top.theillusivec4.curios:curios-forge:5.5.0+1.20.1')
    
    implementation fg.deobf('mezz.jei:jei-1.20.1-common-api:15.2.0.27')
    implementation fg.deobf('mezz.jei:jei-1.20.1-forge-api:15.2.0.27')
    runtimeOnly fg.deobf('mezz.jei:jei-1.20.1-forge:15.2.0.27')

    extra fg.deobf("curse.maven:botanic-additions-310637:6264713") // BotanicAdditions-1.20.1-20.1.3.jar
    extra fg.deobf("curse.maven:hammer-lib-247401:6337292") // HammerLib-1.20.1-20.1.50.jar
    extra fg.deobf("curse.maven:feywild-484377:5900150") // Feywild-1.20.1-4.0.4.jar
    extra fg.deobf("curse.maven:geckolib-388172:6658942") // geckolib-forge-1.20.1-4.7.2.jar
}

mod.run.existing('botania')

compileJava.options.compilerArgs += [
        '-Amod.properties.strict_onlyin=true',
        '-Amod.properties.strict_super=true',
        '-Amod.properties.force_findbugs=true'
]

minecraft {
    runs {
        data {
            mods {
                "${mod.modid}" {
                    source sourceSets.extra
                }
            }
        }
    }
}
